//file:noinspection ConfigurationAvoidance
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.hibernate.orm' version '6.5.2.Final'
    id "org.openjfx.javafxplugin" version "0.1.0"
    id "application"
}

group = 'com.wedasoft'
version = '2.3.1'

repositories {
    mavenCentral()
    // mavenLocal()
}

ext {
    javaFxVersion = "17.0.10"
    javaVersion = 17
    gradleTaskGroupName = 'application javafx spring boot'
}

dependencies {
    // Spring boot starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Database
    implementation 'com.h2database:h2'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JavaFX
    implementation "org.openjfx:javafx-controls:${javaFxVersion}"
    implementation "org.openjfx:javafx-fxml:${javaFxVersion}"
    implementation "org.openjfx:javafx-base:${javaFxVersion}"
    implementation "org.openjfx:javafx-graphics:${javaFxVersion}"

    // WedasoftFX
    implementation 'com.wedasoft:wedasoftfxguicommons:1.0.0'
    implementation 'com.wedasoft:wedasoftfxtestbase:1.0.0'
}

application {
    mainClass = 'com.wedasoft.javafxspringbootgradleapp.JfxSpringBootAppLauncher'
}

javafx {
    version = "${javaFxVersion}"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

hibernate {
    enhancement {
        enableAssociationManagement = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

/* Custom tasks */
task cleanBuildDir(type: DefaultTask) {
    group = "${gradleTaskGroupName}"
    dependsOn clean
}

task runJfxSpringBootApp(type: DefaultTask) {
    group = "${gradleTaskGroupName}"
    dependsOn bootRun
}

task buildExecutableJar(type: DefaultTask) {
    group = "${gradleTaskGroupName}"
    dependsOn bootJar
}

task packageAsAppImage(type: Exec) {
    group = "${gradleTaskGroupName}"
    dependsOn buildExecutableJar

    doFirst {
        println "Loading Spring Boot application.properties..."
        final def applicationProperties = loadSpringApplicationProperties()
        println "Spring Boot application.properties loaded."

        println "Find and validate main jar..."
        final def mainJar = findAndValidateMainJar()
        println "Main jar found. Path: ${mainJar.toString()}"

        println "Creating and preparing output dir..."
        final def outputDir = createAndPrepareOutputDir()
        println "Output dir created. Path: ${outputDir.toString()}"

        println("Creating JPackage command...")
        def jPackageCommand = [
                "jpackage",
                "--type", "app-image",
                "--input", file("$buildDir/libs").toString(),
                "--main-jar", mainJar.name,
                "--dest", outputDir.toString(),
                "--name", (applicationProperties['spring.application.name'] ?: 'DefaultAppName'),
                "--app-version", version
        ]
        println "JPackage command created. Command without double quotes: ${jPackageCommand.join(' ')}"

        println "Executing JPackage command via commandLine..."
        commandLine jPackageCommand
        println "JPackage command executed."
    }
}

private Properties loadSpringApplicationProperties() {
    def applicationProperties = new Properties()
    file('src/main/resources/application.properties').withInputStream { applicationProperties.load(it) }
    return applicationProperties
}

private File findAndValidateMainJar() {
    def mainJar = file("$buildDir/libs").listFiles().find { it.name.endsWith('.jar') }
    if (mainJar == null) {
        throw new GradleException("No JAR file found in build/libs.")
    }
    return mainJar
}

private File createAndPrepareOutputDir() {
    File jPackageOutputDir = file("$buildDir/jpackage") as File
    if (jPackageOutputDir.exists()) {
        jPackageOutputDir.deleteDir()
    }
    jPackageOutputDir.mkdirs()
    return jPackageOutputDir
}
