//file:noinspection ConfigurationAvoidance
//file:noinspection GrMethodMayBeStatic

import java.util.stream.Collectors

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.hibernate.orm' version '6.5.2.Final'
    id "org.openjfx.javafxplugin" version "0.1.0"
    id "application"
}

group = 'com.wedasoft'
version = '2.5.0'

repositories {
    mavenCentral()
    // mavenLocal()
}

ext {
    javaFxVersion = "17.0.10"
    javaVersion = 17
    customTasksGradleGroupName = '_custom tasks (javafx spring boot application)'
}

dependencies {
    // Spring boot starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Database
    implementation 'com.h2database:h2'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JavaFX
    implementation "org.openjfx:javafx-controls:${javaFxVersion}"
    implementation "org.openjfx:javafx-fxml:${javaFxVersion}"
    implementation "org.openjfx:javafx-base:${javaFxVersion}"
    implementation "org.openjfx:javafx-graphics:${javaFxVersion}"

    // WedasoftFX
    implementation 'com.wedasoft:wedasoftfxguicommons:1.0.0'
    implementation 'com.wedasoft:wedasoftfxtestbase:1.0.0'
}

application {
    mainClass = 'com.wedasoft.javafxspringbootgradleapp.JfxSpringBootAppLauncher'
}

javafx {
    version = "${javaFxVersion}"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

hibernate {
    enhancement {
        enableAssociationManagement = true
    }
}

//noinspection GroovyAssignabilityCheck
tasks.named('test') {
    //noinspection GrUnresolvedAccess
    useJUnitPlatform()
}

//============================================================
// CUSTOM TASKS
//============================================================
task cleanBuildDir(type: DefaultTask) {
    group = "${customTasksGradleGroupName}"
    dependsOn clean
}

task runJfxSpringBootApp(type: DefaultTask) {
    group = "${customTasksGradleGroupName}"
    dependsOn bootRun
}

task buildExecutableJar(type: DefaultTask) {
    group = "${customTasksGradleGroupName}"
    dependsOn cleanBuildDir, bootJar
    mustRunAfter(cleanBuildDir)
}

task packageAsAppImage(type: Exec) {
    group = "${customTasksGradleGroupName}"
    dependsOn buildExecutableJar

    doFirst {
        final def applicationProperties = loadSpringApplicationProperties()
        final def mainJar = findAndValidateMainJar()
        final def outputDir = createAndPrepareOutputDir()
        final def appIconFilePath = getAppIconFilePath()
        printJavaVersionInfoForJpackage()

        println("Creating JPackage command...")
        ArrayList<String> jPackageCommand = [
                "jpackage",
                "--type", "app-image",
                "--input", new File(getBuildDir(), "libs").toString(),
                "--main-jar", mainJar.name,
                "--dest", outputDir.toString(),
                "--name", (applicationProperties['spring.application.name'] ?: 'DefaultAppName'),
                "--app-version", version,
                "--icon", appIconFilePath
        ]
        println "\tJPackage command created."
        println "\tUsed command without double quotes: ${jPackageCommand.join(' ')}"

        println "Executing JPackage command via commandLine..."
        commandLine jPackageCommand
        println "\tJPackage command executed."
        println "Packaging as app image finished."
    }
}

private Properties loadSpringApplicationProperties() {
    println "Loading Spring Boot application.properties..."
    def applicationProperties = new Properties()
    file('src/main/resources/application.properties').withInputStream { applicationProperties.load(it) }
    println "\tSpring Boot application.properties loaded."
    return applicationProperties
}

private File findAndValidateMainJar() {
    println "Find and validate main jar..."
    def mainJar = new File(getBuildDir(), "libs").listFiles().find { it.name.endsWith('.jar') }
    if (mainJar == null) {
        throw new GradleException("No JAR file found in build/libs.")
    }
    println "\tMain jar found."
    println "\tPath: ${mainJar.toString()}"
    return mainJar
}

private File createAndPrepareOutputDir() {
    println "Creating and preparing output dir..."
    File jPackageOutputDir = new File(getBuildDir(), "jpackage")
    if (jPackageOutputDir.exists()) {
        jPackageOutputDir.deleteDir()
    }
    jPackageOutputDir.mkdirs()
    println "\tOutput dir created."
    println "\tPath: ${jPackageOutputDir.toString()}"
    return jPackageOutputDir
}

private String getAppIconFilePath() {
    println "Loading app icon..."
    println "\tHint: Use '.ico' as preferred file format. Other formats may not be supported correctly."
    final String resourcePath = "src/main/resources/assets/app-icon.ico"
    File file = project.file(resourcePath) as File
    if (!file.exists() || !file.isFile()) {
        println "\tWARN: No app icon file found at path: '${file.getCanonicalPath()}'."
        println "\tWARN: App icon is not used."
        return ""
    }
    println "\tApp icon loaded.\n\tPath: ${file.getCanonicalPath()}"
    return file.getCanonicalPath()
}

private File getBuildDir() {
    return layout.getBuildDirectory().getAsFile().get() as File
}

private void printJavaVersionInfoForJpackage() {
    println "Determining Java version for using JPackage..."
    def process = "java --version".execute()
    def output = new StringBuffer()
    process.consumeProcessOutput(output, new StringBuffer())
    process.waitFor()

    def javaVersionString = Arrays.stream(output.toString().split("\n"))
            .filter { !it.isBlank() }
            .collect(Collectors.joining("\n\t"))
    println "\tJava version determined via 'java --version':"
    println "\t${javaVersionString}"
}
